stage('dev') {
    node{
        checkout scm

        openshift.withProject( "todo-app-dev" ) {
            def buildTemplate = openshift.process("-f infra/yml/s2i-todo-app-build-template.yml")
            openshift.apply(buildTemplate)

            openshift.startBuild("todo-app", "--wait=true")

            def deploymentTemplate = openshift.process("-f infra/yml/todo-app-deployment-template.yml",
                    "-p", "URL=todo-app-dev.192.168.99.101.nip.io")
            openshift.apply(deploymentTemplate)

            openshift.selector("dc/todo-app").rollout().latest()
            openshift.selector("dc/todo-app").rollout().status("--watch=true")

            openshift.tag("todo-app-dev/todo-app:latest todo-app-int/todo-app:latest")
        }
    }
}


node{
    checkout scm
    openshift.withProject( "todo-app-int" ) {

        stage('build-e2e-test-containers'){
            def checkLayoutBuild = startE2ETestBuild('check-layout','e2e/layout')
            def checkLifeCycle = startE2ETestBuild('check-life-cycle','e2e/lifeCycle')
            waitFor(checkLayoutBuild)
            waitFor(checkLifeCycle)
        }

        stage('deploy-to-int'){
            def deploymentTemplate = openshift.process("-f infra/yml/todo-app-deployment-template.yml",
                    "-p", "URL=todo-app-int.192.168.99.101.nip.io")
            openshift.apply(deploymentTemplate)

            openshift.selector("dc/todo-app").rollout().latest()
            openshift.selector("dc/todo-app").rollout().status("--watch=true")
        }

        stage('run-e2e-tests'){
            deletePod('check-layout')
            deletePod('check-life-cycle')
            startE2ETest('check-layout')
            startE2ETest('check-life-cycle')
            waitFor(openshift.selector('po/check-layout').object())
            waitFor(openshift.selector('po/check-life-cycle').object())
        }

        stage('pre-production'){
            input "Deploy to production?"

            openshift.tag("todo-app-int/todo-app:latest todo-app-prod/todo-app:latest")
        }
    }
}



stage('prod') {
    node{
        openshift.withProject( "todo-app-prod" ) {
            def deploymentTemplate = openshift.process("-f infra/yml/todo-app-deployment-template.yml")
            openshift.apply(deploymentTemplate)

            openshift.selector("dc/todo-app").rollout().latest()
            openshift.selector("dc/todo-app").rollout().status("--watch=true")
        }
    }
}


private Object startE2ETestBuild(def buildName, def contextDir) {
    def buildTemplate = openshift.process("-f infra/yml/s2i-sakuli-build-template.yml",
            "-p", "IMAGE=" + buildName,
            "-p", "TESTSUITE_REPOSITORY_URL=http://gogs.192.168.99.101.nip.io/sven/todo-app.git",
            "-p", "TESTSUITE_CONTEXT_DIR="+ contextDir)
    openshift.apply(buildTemplate)
    return openshift.startBuild(buildName)
}

private void startE2ETest(def testName) {
    openshift.run(testName,
            "--image=172.30.1.1:5000/todo-app-int/"+ testName + ":latest",
            "--restart=Never",
            "-l application="+testName)
}


private void deletePod(def podName){
    def pod = openshift.selector("po/"+podName)
    if(pod.exists()){
        pod.delete()
    }
}

private static void waitFor(def selector){
    selector.watch {
        if ( it.count() == 0 ) return false

        def allDone = true
        it.withEach {
            def model = it.object()
            if ( model.status.phase != "Complete" && model.status.phase != "Completed" ) {
                allDone = false
            }
        }

        return allDone
    }
}